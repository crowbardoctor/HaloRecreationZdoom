ACTOR FragGrenadeProjectile
{
  Radius 8
  Height 8
  Speed 20
  Projectile
  -NOGRAVITY
  +RANDOMIZE
  //+NOFRICTIONBOUNCE
  BounceType "Doom"
  BounceFactor 0.4 
  ProjectileKickBack 15
  WallBounceFactor 0.5
  Gravity 0.5
  Activation THINGSPEC_Activate   
  +USESPECIAL 
  BounceSound "fragboun"
  Obituary "$OB_GRENADE" // "%o caught %k's grenade."
  DamageType Grenade
  States
  {
  Spawn:
	TNT1 A 0 A_Jump(50,"Spawn2")
	TNT1 A 0 A_Jump(70,"Spawn3")
	TNT1 A 0 A_Jump(128,"Spawn4")
    FRAG AABBCCDDEEFFGGHH 1 A_SpawnItem("SmokeTrail",0,4.0)
    Loop
  Spawn2:
    FRAG AABBCCDDEEFFGGHH 2 A_SpawnItem("SmokeTrail",0,4.0)
    Loop
  Spawn3:
    FRAG AHGFEDCB 2 A_SpawnItem("SmokeTrail",0,4.0)
    Loop
  Spawn4:
    FRAG AAHHGGFFEEDDCCBB 2 A_SpawnItem("SmokeTrail",0,4.0)
    Loop
  Death:
	TNT1 A 0 A_Jump(128,"Death2")
    FRAG G 15
	Goto FinalDeath
  Death2:
    FRAG C 15
  FinalDeath:
    TNT1 A 0 Bright A_SpawnItem("FragExplosion",0,18.0)
    Stop
  Active:
    PLAY A 4 Bright A_SpawnItem("MineSingle")
    Stop
  }
}
ACTOR FragExplosion
{
  scale 1.75
  Obituary "$OB_GRENADE" // "%o caught %k's grenade."
  +NOGRAVITY
  States
  {
  Spawn:
	TNT1 A 0
    MISL B 4 Bright A_Explode(250,280)
    MISL C 6 Bright A_PlaySound("world/barrelx")
    MISL D 7 Bright 
    Stop
  }
}

ACTOR MineSpawned : SwitchingDecoration
{
  Height 8
  Radius 8
  Activation THINGSPEC_Activate   
  +USESPECIAL 
  +Friendly
  -NOGRAVITY
  States
  {
  Spawn:
		TNT1 A 0 A_JumpIfCloser(200.0,"Friendly")
    GMIN B 4
    GMIN A 4 Bright
		TNT1 A 0 A_LookEx(0,0,128.0,1.0,360.0,"FinalDeath")
    Loop
  Active:
    GMIN A 4 Bright A_SpawnItemEx("MineSingle",1,AAPTR_TARGET)
    Stop
  Friendly:
    GMIN C 5 Bright
	Goto Spawn	
  FinalDeath:
	GMIN A 3 Bright A_PlaySound("laserlck")
	GMIN B 3 A_PlaySound("laserlck")
	GMIN A 3 Bright A_PlaySound("laserlck")
	GMIN B 3 A_PlaySound("laserlck")
	GMIN A 3 Bright A_PlaySound("laserlck")
	GMIN B 3 A_PlaySound("laserlck")
	GMIN A 3 Bright A_PlaySound("laserlck")
    TNT1 A 0 Bright A_SpawnItem("MineExplosion",0,18.0)
    Stop
  }
}

ACTOR MineProjectile
{
  Radius 8
  Height 8
  Speed 17
  Projectile
  -NOGRAVITY
  BounceType "Doom"
  BounceFactor 0.4 
  Friction 0.5
  ProjectileKickBack 15
  WallBounceFactor 0.5
  Gravity 0.5
  BounceSound "fragboun"
  States
  {
  Spawn:
    GMIN B 1
    Loop
  Death:
    TNT1 A 0 Bright A_SpawnItem("MineSpawned")
	stop
  }
}

ACTOR MineExplosion
{
  scale 1
  Obituary "$OB_GRENADE" // "%o caught %k's grenade."
  +NOGRAVITY
  States
  {
  Spawn:
	TNT1 A 0
    MISL B 4 Bright A_Explode(160,200,0,true)
    MISL C 6 Bright A_PlaySound("world/barrelx")
    MISL D 7 Bright
    Stop
  }
}

//==================
// ROCKET
//==================

ACTOR Rocket2
{
  Radius 11
  Height 8
  Speed 20
  Damage 20
  Projectile
  +RANDOMIZE
  +DEHEXPLOSION
  SeeSound "weapons/rocklf"
  DeathSound "weapons/rocklx"
  Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
  States
  {
  Spawn:
    MISL A 1 Bright
		TNT1 A 0 A_SpawnItem("RocketSmokeTrail2",0,4.0)
    MISL A 1 Bright
    Loop
  Death:
    MISL B 8 Bright A_Explode
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}
Actor Rocket2Homing : Rocket2
{
  Speed 12
  +SEEKERMISSILE
  States
  {  
  Spawn:
    MISL A 1 Bright A_SeekerMissile(0,2,SMF_LOOK,256,20)
	MISL A 1 Bright A_SpawnItem("RocketSmokeTrail2",0,4.0)
		TNT1 A 0 A_SpawnItemEx("Rocket2HomingLaserSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS )
	MISL A 1 Bright
	{
		if(tracer)
		{
			if(!countinv("BoolFoundTarget"))
			{
				A_PlaySound("laserlck");
			}
			A_GiveInventory("BoolFoundTarget");
		}
	}
    Loop
  }
}

ACTOR Rocket2HomingLaserSpawner
{
  States
  {
  Spawn:
	TNT1 A 4
	TNT1 A 1 
	{
		if(tracer)
		{
			A_Warp(AAPTR_TRACER);
			A_SpawnItemEx("Rocket2HomingLaser",0,0,35.0,0,0,0,0,SXF_TRANSFERPOINTERS);
		}
	}
    Stop
  }
}
ACTOR Rocket2HomingLaser
{
  +NOGRAVITY
  RenderStyle Add
  States
  {
  Spawn:
	TNT1 A 1
    LASD A 4 Bright 
    Stop
  }
}

ACTOR PlasmaBallHalo : PlasmaBall
{
  +SEEKERMISSILE
  Scale 0.8
  Speed 40
  Alpha 0.8
  SeeSound ""
  States
  {
  Spawn:
    PLSS AB 6 Bright A_SeekerMissile(0, 2)
    Loop
  Death:
    PLSE ABCDE 4 Bright
    Stop
  }
}
ACTOR PlasmaBallHaloLarge : PlasmaBallHalo
{
  Scale 1.2
  Speed 12
  States
  {
  Spawn:
    PLSS AB 6 Bright A_SeekerMissile(0, 2)
		TNT1 A 0 A_SeekerMissile(0, 2)
		TNT1 A 0 A_SeekerMissile(0, 2)
    Loop
  }
}
ACTOR PlasmaBallHaloNormal : PlasmaBallHalo
{
  Scale 1
  Speed 25
}

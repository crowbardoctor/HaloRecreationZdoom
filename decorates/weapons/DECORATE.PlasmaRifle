ACTOR PlasmaRifle2Pickup : GunPickup
{
  states
  {
  Spawn:
    PLAS A -1
    stop
  Active:
    PLAS A 4 Bright A_SpawnItemEx ("PlasmaRifle2Giver",1,AAPTR_TARGET)
    stop
  }
}
ACTOR PlasmaRifle2Giver : GunGiver
{
  states
  {
  Pickup:
    PLAS A 1 ACS_NamedExecute("PickupPlasmaRifle2",0)
    stop
  }
}
Actor PlasmaRifleHeat : Inventory
{
  Inventory.MaxAmount 100
  -INVENTORY.INVBAR
}

ACTOR PlasmaRifle2 : HaloWeapon
{
  Weapon.AmmoUse 1
  Weapon.AmmoType "Cell"
  Inventory.PickupMessage "$GOTPLASMA"
  Tag "$TAG_PLASMARIFLE"
  States
  {
  WeaponReady:
    PLSG A 1 A_WeaponReady
	Goto Ready
  Empty:
    PLSG A 1 A_WeaponReady(WRF_NOFIRE)
	Goto Ready
  Deselect:
    PLSG A 1 A_Lower
    Loop
  WeaponSelect:
    PLSG A 1 A_Raise
    Loop
	
  MeleeStart:
	PLSL AB 2
	TNT1 A 2
	Goto PreMelee
  MeleeEnd:
	PLSM FE 2
	Goto Ready
	
  Melee:
    PLSM A 3 A_PlaySound("meleeswish",CHAN_WEAPON,0.25)
    PLSM B 2 A_CustomPunch(20,true,0,"BulletPuff",64,0,0,"BasicArmorBonus","player/male/fist")
    PLSM C 2
    PLSM D 1
    PLSM EF 2
	TNT1 A 4 
	TNT1 A 4 
	PLSG A 0 A_JumpIfInventory("IsMelee",1,"PreMelee")
	Goto MeleeEnd

  WeaponFireEmpty:
	PLSG D 2 A_PlaySound("dryfire",CHAN_WEAPON,0.35)
		TNT1 A 0 ACS_NamedExecute("WeaponEmpty",0)
	PLSG A 4 
		TNT1 A 0 A_Refire("Fire")
	Goto Ready
	
  WeaponFire:
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_JumpIfInventory("PlasmaRifleHeat",100,"Overheat")
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("DSPLASMB",CHAN_WEAPON,1.0)
	TNT1 A 0 A_JumpIfInventory("PlasmaRifleHeat",33,"Fire3")
	TNT1 A 0 A_JumpIfInventory("PlasmaRifleHeat",12,"Fire2")
	TNT1 A 0 A_JumpIfInventory("PlasmaRifleHeat",8,"Fire1")
  Fire1:
    PLSG A 5 A_FireCustomMissile("PlasmaBallHaloLarge")
	TNT1 A 0 A_GiveInventory("PlasmaRifleHeat",4)
	Goto Heat
  Fire2:
    PLSG A 4 A_FireCustomMissile("PlasmaBallHaloNormal")
	Goto Heat
  Fire3:
    PLSG A 3 A_FireCustomMissile("PlasmaBallHalo")
	Goto Heat
	
  Heat: 
	TNT1 A 0 A_GiveInventory("PlasmaRifleHeat",10)
		TNT1 A 0 A_Refire("FireAuto")
	Goto Ready
	
  Overheat: 
	PLSG B 2
		TNT1 A 0 A_PlaySound("overheat",CHAN_7 )
  Overheat2: 
	PLSG C 2 A_JumpIf(countinv("PlasmaRifleHeat") == 0,"Overheat3")
		TNT1 A 0 A_SpawnItemEx("OverheatParticle",10,0,36,random(-10,10),random(8,-8),random(4,5),SXF_ABSOLUTEANGLE ,0)
		TNT1 A 0 A_SpawnItemEx("OverheatParticle",10,0,36,random(-10,10),random(8,-8),random(4,5),SXF_ABSOLUTEANGLE ,0)
		TNT1 A 0 A_SpawnItemEx("OverheatParticle",10,0,36,random(-10,10),random(8,-8),random(4,5),SXF_ABSOLUTEANGLE ,0)
		TNT1 A 0 A_QuakeEx( 1, 1, 1, 2, 0, 12,"" )
	Loop
  Overheat3: 
	PLSG B 2
		TNT1 A 0 A_Refire("Fire")
	Goto Ready
	
  Flash:
    PLSF AB 1 Bright A_Light1
    Goto LightDone
  Spawn:
    PLAS A -1
    Stop
  }
}

Actor PlasmaRifle2Item replaces PlasmaRifle
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx ("PlasmaRifle2Pickup")
			TNT1 A 0 A_SpawnItemEx ("Cell",0,0,0,random(-5,5),random(-5,5))
			stop
	}
}
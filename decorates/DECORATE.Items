actor PowerQuadDamage : PowerDamage
{
	damagefactor "normal", 4
	Powerup.Duration -35
//	inventory.icon "QUADE0"
	Powerup.Color "50 00 50"
}

ACTOR QuadDamage : CustomInventory replaces Berserk
{
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage "Quad Damage!" // "Berserk!"
  Inventory.PickupSound "quadpick"
  States
  {
  Spawn:
    QUAD ABCDCB 2
    Loop
  Pickup:
    TNT1 A 0 A_GiveInventory("PowerQuadDamage")
    TNT1 A 0 
    Stop
  }
}

ACTOR Megasphere2 : CustomInventory replaces Megasphere
{
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage "$GOTMSPHERE" // "MegaSphere!"
  Inventory.PickupSound "misc/p_pkup"
  States
  {
  Spawn:
    ZOUL ABCD 6 Bright
    Loop
  Pickup:
    TNT1 A 0 ACS_NamedExecute("SetShieldAndHealth",0,300,100)
    Stop
  }
}

ACTOR FragGrenade : ammo replaces GreenArmor
{
  Inventory.PickupMessage "Picked up a grenade." 
  Inventory.Amount 1
  Inventory.MaxAmount 3
  Ammo.BackpackAmount 2
  Ammo.BackpackMaxAmount 6
  Inventory.Icon "FRAGA0"
  States
  {
  Spawn:
    FRAG A -1
    Stop
  }
}

ACTOR Mine : ammo replaces BlueArmor
{
  Inventory.PickupMessage "Picked up 3 mines." 
  Inventory.Amount 3
  Inventory.MaxAmount 6
  Ammo.BackpackAmount 6
  Ammo.BackpackMaxAmount 12
  Inventory.Icon "GMINA0"
  States
  {
  Spawn:
    GMIP A -1
    Stop
  }
}
ACTOR MineSingle : Mine
{
  radius 64
  -SOLID
  Inventory.PickupMessage "Picked up a mine." 
  Inventory.Amount 1
  States
  {
  Spawn:
    GMIN A -1
    Stop
  }
}
Actor OrgClip : Clip {}
ACTOR ClipChooser replaces Clip
{
	States
	{
	Spawn:
		TNT1 A 3
		TNT1 A 1 A_JumpIf(CallACS("hasChaingunClose",0,0,0,0) == 1,"SuperClip")
		TNT1 A 1 A_SpawnItemEx("SmallClip",0,0,5.0)
		stop
	SuperClip:
		TNT1 A 1 A_SpawnItemEx("OrgClip",0,0,5.0)
		stop
	}	
}
ACTOR SmallClip : Ammo
{
  Inventory.PickupMessage "Picked up magnum bullets." 
  Inventory.Amount 5
  Inventory.MaxAmount 200
  Ammo.BackpackAmount 5
  Ammo.BackpackMaxAmount 400
  Inventory.Icon "CLIPB0"
  States
  {
  Spawn:
    CLIP B -1
    Stop
  }
}

ACTOR ShotgunChooser replaces Shotgun
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_JumpIf(CallACS("NotRandomPlacement",0,0,0,0) == 0,"Other")
		TNT1 A 1 A_SpawnItemEx("Shotgun2Item",0,0,0)
		stop
	Other:
		TNT1 A 1 A_SpawnItemEx("SuperShotgun2Item",0,0,0.0)
		stop
	}	
}
Actor OrgShell : Shell{}
ACTOR ShellChooser replaces Shell
{
	States
	{
	Spawn:
		TNT1 A 3
		TNT1 A 1 A_JumpIf(CallACS("hasSuperShotgunClose",0,0,0,0) == 1,"SuperShell")
		TNT1 A 1 A_SpawnItemEx("OrgShell",0,0,5.0)
		stop
	SuperShell:
		TNT1 A 1 A_SpawnItemEx("SuperShell",0,0,5.0)
		stop
	}	
}
Actor OrgCell : Cell
{
  Inventory.Amount 50
}
ACTOR CellChooser replaces Cell
{
	States
	{
	Spawn:
		TNT1 A 3
		TNT1 A 1 A_JumpIf(CallACS("hasBFGClose",0,0,0,0) == 1,"BFGCell")
		TNT1 A 1 A_SpawnItemEx("OrgCell",0,0,5.0)
		stop
	BFGCell:
		TNT1 A 1 A_SpawnItemEx("BFGCell",0,0,5.0)
		stop
	}	
}

ACTOR ShellBox2 : CustomInventory replaces ShellBox
{
  Inventory.PickupMessage "$GOTSHELLBOX" // "Picked up a box of shotgun shells."
  States
  {
  Spawn:
    SBOX B -1
    Stop
  Pickup:
	TNT1 A 0
	TNT1 A 0 A_JumpIf(CallACS("ShellBoxCheck",0,0,0,0) == 0,"PickupFail")
    TNT1 A 0 A_GiveInventory("Shell",20)
    TNT1 A 0 A_GiveInventory("SuperShell",20)
	stop
  PickupFail:
	TNT1 A 0
	fail
  }
}

ACTOR SuperShell : Ammo
{
  Inventory.PickupMessage "Picked up super shotgun shells."
  Inventory.Amount 4
  Inventory.MaxAmount 50
  Ammo.BackpackAmount 4
  Ammo.BackpackMaxAmount 100
  Inventory.Icon "SHELB0"
  States
  {
  Spawn:
    SHEL B -1
    Stop
  }
}
ACTOR BFGCell : Ammo
{
  Inventory.PickupMessage "Picked up a bfg energy cell." // "Picked up an energy cell."
  Inventory.Amount 2
  Inventory.MaxAmount 15
  Ammo.BackpackAmount 2
  Ammo.BackpackMaxAmount 30
  Inventory.Icon "CELLB0"
  States
  {
  Spawn:
    CELL B -1
    Stop
  }
}

//ACTOR OrgHealthBonus : HealthBonus{}
ACTOR HealthBonus2 : Health
{
  +COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.Amount 5
  Inventory.MaxAmount 200
  Inventory.PickupMessage "$GOTHTHBONUS" // "Picked up a health bonus."
  States
  {
  Spawn:
    BON3 ABCDCB 6
    Loop
  }
}

ACTOR BackpackOrg : Backpack {}
ACTOR BackpackACS : CustomInventory replaces Backpack
{
  Inventory.PickupMessage "$GOTBACKPACK" // "Picked up a box of shotgun shells."
  States
  {
  Spawn:
    BPAK A -1
    Stop
  Pickup:
	TNT1 A 0
	TNT1 A 0 ACS_NamedExecute("AmmoCapacity",0,0,0,0)
    TNT1 A 0 A_GiveInventory("BackpackOrg",1)
	stop
  PickupFail:
	TNT1 A 0
	fail
  }
}

/*
ACTOR HealthBonusChooser replaces HealthBonus
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_JumpIf(CallACS("NotRandomPlacement",0,0,0,0) == 0,"Other")
		TNT1 A 1 A_SpawnItemEx("OrgHealthBonus",0,0,5.0)
		stop
	Other:
		TNT1 A 1 A_SpawnItemEx("HealthBonus2",0,0,5.0)
		stop
	}	
}
*/
